import de.undercouch.gradle.tasks.download.Download

buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "de.undercouch.download" version "3.3.0"
}

apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile "com.google.apis:google-api-services-drive:v3-rev129-1.25.0"
    compile "com.google.apis:google-api-services-sheets:v4-rev492-1.23.0"
    compile "com.google.apis:google-api-services-slides:v1-rev294-1.23.0"
    compile "com.google.cloud:google-cloud-vision:1.12.0"
    compile "com.squareup.okhttp3:okhttp:3.11.0"
    compile "commons-io:commons-io:2.6"
    compile "io.dropwizard:dropwizard-assets:1.2.1"
    compile "io.github.openfeign:feign-jackson:10.0.1"
    compile "io.github.openfeign.form:feign-form:3.3.0"
    compile "org.apache.commons:commons-text:1.4"
    compile "org.apache.httpcomponents:httpmime:4.5.6"
    compile "org.apache.pdfbox:pdfbox:2.0.11"
    compile "org.bytedeco.javacpp-presets:leptonica:1.76.0-1.4.3:$platform"
    compile "org.bytedeco.javacpp-presets:tesseract:4.0.0-rc2-1.4.3"
    compile "org.bytedeco.javacpp-presets:tesseract:4.0.0-rc2-1.4.3:$platform"
    compile "org.glassfish.jersey.media:jersey-media-multipart:2.25.1"
    compile "org.jsoup:jsoup:1.10.3"
    compile "org.openpnp:opencv:3.2.0-1"
    compile "org.projectlombok:lombok:1.18.2"

    testCompile "junit:junit:4.12"
    testCompile "org.assertj:assertj-core:3.11.0"
}

configurations.all {
    exclude group: "com.google.guava", module: "guava-jdk5"
}

task downloadFiles(type: Download) {
    src([
        "http://norvig.com/ngrams/count_1w.txt",
        "http://norvig.com/ngrams/count_2w.txt",
        "http://svn.code.sf.net/p/cmusphinx/code/trunk/cmudict/sphinxdict/cmudict.0.7a_SPHINX_40",
        "https://github.com/tesseract-ocr/tessdata_fast/raw/4.0.0/eng.traineddata",
        // hardcoded training data until Tesseract 4 supports tessedit_char_whitelist
        // https://github.com/tesseract-ocr/tesseract/issues/751
        "https://github.com/Shreeshrii/tessdata_shreetest/raw/b69b7e6ba6c7b0bd15f1b5541ac8fa5746383ad4/digits.traineddata"
    ])
    dest "data"
}

task run(type: JavaExec) {
    classpath project.sourceSets.main.runtimeClasspath
    main "com.kyc.snap.server.SnapServer"
}

task generateWikinet(type: JavaExec) {
    classpath project.sourceSets.main.runtimeClasspath
    main "com.kyc.snap.wikinet.Wikinet"
}

task runOcrTest(type: JavaExec) {
    classpath project.sourceSets.main.runtimeClasspath
    main "com.kyc.snap.opencv.OcrTest"
}

