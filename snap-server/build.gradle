import de.undercouch.gradle.tasks.download.Download

buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "de.undercouch.download" version "3.3.0"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'antlr'

sourceCompatibility = 11

repositories {
    jcenter()
}

dependencies {
    antlr "org.antlr:antlr4:4.9.2"

    compile 'com.github.ben-manes.caffeine:caffeine:3.0.3'
    compile "com.github.kklisura.cdt:cdt-java-client:2.0.0"
    compile "com.google.apis:google-api-services-drive:v3-rev129-1.25.0"
    compile "com.google.apis:google-api-services-sheets:v4-rev492-1.23.0"
    compile "com.google.apis:google-api-services-slides:v1-rev294-1.23.0"
    compile "com.hubspot.slack:slack-java-client:1.1"
    compile "com.squareup.okhttp3:okhttp:3.11.0"
    compile "commons-io:commons-io:2.6"
    compile "io.dropwizard:dropwizard-assets:1.2.1"
    compile "io.github.openfeign:feign-jackson:10.0.1"
    compile "io.github.openfeign.form:feign-form:3.3.0"
    compile "jakarta.xml.bind:jakarta.xml.bind-api:2.3.2"
    compile "one.util:streamex:0.7.2"
    compile "org.apache.commons:commons-text:1.4"
    compile "org.apache.httpcomponents:httpmime:4.5.6"
    compile "org.apache.pdfbox:pdfbox:2.0.11"
    compile "org.bytedeco.javacpp-presets:leptonica:1.75.3-1.4.1:$platform"
    compile "org.bytedeco.javacpp-presets:opencv:3.4.1-1.4.1"
    compile "org.bytedeco.javacpp-presets:opencv:3.4.1-1.4.1:$platform"
    compile "org.bytedeco.javacpp-presets:tesseract:3.05.01-1.4.1"
    compile "org.bytedeco.javacpp-presets:tesseract:3.05.01-1.4.1:$platform"
    compile "org.glassfish.jaxb:jaxb-runtime:2.3.2"
    compile "org.glassfish.jersey.media:jersey-media-multipart:2.25.1"
    compile "org.jsoup:jsoup:1.10.3"
    compile "org.projectlombok:lombok:1.18.20"

    testCompile "junit:junit:4.12"
    testCompile "org.assertj:assertj-core:3.11.0"
}

generateGrammarSource {
    outputDirectory = file("src/main/java/com/kyc/snap/antlr")
}

task downloadWordFiles(type: Download) {
    src([
        "http://norvig.com/ngrams/count_1w.txt",
        "http://norvig.com/ngrams/count_2w.txt",
        "http://svn.code.sf.net/p/cmusphinx/code/trunk/cmudict/sphinxdict/cmudict.0.7a_SPHINX_40"
    ])
    dest "data"
}

task downloadTesseractFiles(type: Download) {
    // TODO: This training data is better, but I don't know where I got it from:
    // https://drive.google.com/file/d/1xm710oVrukK3KdTq3xcJ3D__jkJj5EE6/view?usp=sharing
    src "https://github.com/tesseract-ocr/tessdata/raw/3.04.00/eng.traineddata"
    dest "data/tessdata"
}

task downloadFiles(dependsOn: [downloadWordFiles, downloadTesseractFiles]) {}

task run(type: JavaExec) {
    classpath project.sourceSets.main.runtimeClasspath
    main "com.kyc.snap.server.SnapServer"
}

task generateWikinet(type: JavaExec) {
    classpath project.sourceSets.main.runtimeClasspath
    main "com.kyc.snap.wikinet.Wikinet"
}

